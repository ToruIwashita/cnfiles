## git関数用補完
function __git_modified_files() {
  local git_status_res changes_not_staged_str_index

  git_status_res="$(git status)"
  changes_not_staged_str_index=$((${${(f@)git_status_res}[(i)*Changes not staged*]} + 4))

  compadd ${(R)${(M)${${(f@)git_status_res}[$changes_not_staged_str_index,-1]}:#*modified:*}##*: #}
}

function __git_untracked_files() {
  local git_status_res untracked_str_index

  git_status_res="$(git status)"
  untracked_str_index=$((${${(f@)git_status_res}[(i)*Untracked*]} + 3))

  compadd ${(R)${(M)${${(f@)git_status_res}[$untracked_str_index,-1]}##\#*}#\#[[:blank:]]}
}

function __git_branches() {
  compadd ${(R)$(git branch)#\*}
}

function _ga() {
  if [ -d ./.git ]; then
    _arguments : \
      '(:)-m[file]:Input file:__git_modified_files' \
      '(:)-u[file]:Input file:__git_untracked_files' \
      '(-)*:argument:_files'
  else
    _arguments : '(-)*:argument:_files'
  fi
}

function _gd() {
  if [ -d ./.git ]; then
    _arguments : '(-)*:argument:__git_modified_files'
  else
    _arguments : '(-)*:argument:_files'
  fi
}

function _gcheck() {
  if [ -d ./.git ]; then
    _arguments : \
      '(:)-m[file]:Input file:__git_modified_files' \
      '(:)-b[branch]:Input branch:__git_branches' \
      '(-)*:argument:_files'
  else
    _arguments : '(-)*:argument:_files'
  fi
}

function _gsw() {
  if [ -d ./.git ]; then
    _arguments : '(:)*:argument:__git_branches'
  fi
}

function _gud() {
  if [ -d ./.git ]; then
    _arguments : '(-)*:argument:__git_modified_files'
  fi
}

compdef _ga ga
compdef _gd gd
compdef _gcheck gcheck
compdef _gsw gsw
compdef _gud gud
