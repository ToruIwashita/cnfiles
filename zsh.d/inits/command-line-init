## auto-execution of functions
# _precmd-vcs-info
__precmd-vcs-info() {
  vcs_info
  PROMPT_VIM_MODE_COLOR='%{[38;5;33m%}'
}

# precmd_functions設定
precmd_functions+='__precmd-vcs-info'
# chpwd_functions設定
chpwd_functions+='chpwd_recent_dirs'

## widgets
# PROMPT_VIM_MODE_COLOR選択widgets
_zle-keymap-select() {
  case $KEYMAP in
    vicmd)
      PROMPT_VIM_MODE_COLOR="%{[38;5;87m%}"
      ;;
    main|viins)
      PROMPT_VIM_MODE_COLOR="%{[38;5;33m%}"
      ;;
  esac

  zle reset-prompt
}
zle -N zle-keymap-select _zle-keymap-select  # _zle-keymap-selectをzle-keymap-selectに設定
zle -N edit-command-line                     # コマンドラインを$EDITORで編集

## git hooks
# git管理下のディレクトリのみフック関数を呼び出すようにする
function +vi-git-hook-begin() {
  if [[ $(command git rev-parse --is-inside-work-tree 2> /dev/null) != 'true' ]]; then
    # 0以外を返すとそれ以降のフック関数は呼び出されない
    return 1
  fi

  return 0
}

function +vi-git-vcs-info-msg-setting() {
  local relative_root_dir_path current_head origin_head not_pushed_count staged_count modified_count untracked_count

  # vcs_info_msg_1_の場合のみ実行されるようにする(if文の1がmsgの数値に該当)
  if [[ $1 != 1 ]]; then
    return 0
  fi

  relative_root_dir_path=$(git rev-parse --show-cdup 2>/dev/null)
  current_head=$(git rev-parse --abbrev-ref HEAD 2>/dev/null)

  if git rev-parse --abbrev-ref origin/$current_head &>/dev/null; then
    origin_head="origin/$current_head"
  else
    origin_head='origin/HEAD'
  fi

  not_pushed_count=${"$(git rev-list $origin_head..$current_head &>/dev/null | wc -l)"// /}
  staged_count=${"$(git diff --cached --name-only &>/dev/null | wc -l)"// /}
  modified_count=${"$(git ls-files --modified $relative_root_dir_path &>/dev/null | wc -l)"// /}
  untracked_count=${"$(git ls-files --exclude-standard --others $relative_root_dir_path &>/dev/null | wc -l)"// /}

  # hook_com[misc]は%mのフォーマット
  if [[ not_pushed_count -gt 0 ]]; then
    hook_com[misc]+="%f%F{magenta}P: $not_pushed_count%f|"
  else
    hook_com[misc]+="P: $not_pushed_count|"
  fi

  if [[ staged_count -gt 0 ]]; then
    hook_com[misc]+="%f%F{green}S: $staged_count%f|"
  else
    hook_com[misc]+="S: $staged_count|"
  fi

  if [[ modified_count -gt 0 ]]; then
    hook_com[misc]+="%F{red}M: $modified_count%f|"
  else
    hook_com[misc]+="M: $modified_count|"
  fi

  if [[ untracked_count -gt 0 ]]; then
    hook_com[misc]+="%F{red}U: $untracked_count%f"
  else
    hook_com[misc]+="U: $untracked_count"
  fi
}

## zstyle
# vcs_info
zstyle ':vcs_info:*' enable git hg                                                  # git,hgを有効
zstyle ':vcs_info:*' max-exports 2                                                  # ${vcs_info_msg_0_},${vcs_info_msg_1_}にメッセージをエクスポート
zstyle ':vcs_info:*' formats '%s][* %F{green}%b%f'                                  # 通常時のフォーマット
zstyle ':vcs_info:*' actionformats '%s][* %F{green}%b%f(%F{red}%a%f)'               # コンフリクト時など,アクションがある場合のフォーマット
zstyle ':vcs_info:git:*' formats '%s][* %F{green}%b%f' '%m'                         # gitの通常時のフォーマット(引数1つ目vcs_info_msg_0_,2つ目vcs_info_msg_1_)
zstyle ':vcs_info:git:*' actionformats '%s][* %F{green}%b%f(%F{red}%a%f)' '%m'      # gitのコンフリクト時など,アクションがある場合のフォーマット(引数1つ目vcs_info_msg_0_,2つ目vcs_info_msg_1_)
zstyle ':vcs_info:git+set-message:*' hooks git-hook-begin git-vcs-info-msg-setting  # gitのファイル状態をhook_comに格納
# chpwd
zstyle ":chpwd:*" recent-dirs-max 100             # 最近利用したディレクトリ最大保存数
zstyle ":chpwd:*" recent-dirs-default true        # 2つ以上の引数または数値以外の引数が与えられた場合,cdと同じ動作をする
zstyle ":completion:*" recent-dirs-insert always  # recent-dirs-default trueの場合に補完を開始するとディレクトリ名一覧になる
# completion
zstyle ':completion:*' completer _complete _match _ignored _prefix  # コンプリータ指定(通常,パターンマッチ,除外パターン復活,単語途中の補完)
zstyle ':completion:*' menu select interactive                      # menuselect+interactive-mode(補完全般共通)
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}               # 補完候補色付け

## prompt
# プロンプト表示
PROMPT='%{[38;5;33m%}_%{[0m%}
%{[38;5;33m%}┃%{[0m%}[${vcs_info_msg_0_}]:%~/
%{[38;5;33m%}┗━%{[0m%}(%?)$PROMPT_VIM_MODE_COLOR%#%{[0m%} '
RPROMPT='(${vcs_info_msg_1_})%{[38;5;33m%}━%{[0m%}[%D{%T}|%n]'

## command-line
# 色定義
zle_highlight=(region:standout special:standout suffix:bold isearch:bg=magenta,fg=white)

# vim: ft=zsh
