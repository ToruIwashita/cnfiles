# _precmd-vcs-info
__precmd-vcs-info() {
  vcs_info
  PROMPT_VIM_MODE_COLOR='blue'
}
# _chpwd-ls
__chpwd-ls() {
  [[ $dirstack[1]:h != $PWD ]] && ls
}

## auto-execution of functions
# precmd_functions設定
precmd_functions+='__precmd-vcs-info'
# chpwd_functions設定
chpwd_functions=(chpwd_recent_dirs __chpwd-ls)

## widgets
# PROMPT_VIM_MODE_COLOR選択widgets
_zle-keymap-select() {
  case $KEYMAP in
    vicmd)
      PROMPT_VIM_MODE_COLOR="cyan" ;;
    main|viins)
      PROMPT_VIM_MODE_COLOR="blue" ;;
  esac

  zle reset-prompt
}
zle -N zle-keymap-select _zle-keymap-select  # _zle-keymap-selectをzle-keymap-selectに設定
zle -N edit-command-line                     # コマンドラインを$EDITORで編集

## zstyle
# vcs_info
zstyle ':vcs_info:*' enable git hg                                                # git,hgを有効
zstyle ':vcs_info:*' max-exports 2                                                # ${vcs_info_msg_0_},${vcs_info_msg_1_}にメッセージをエクスポート
zstyle ':vcs_info:*' formats '%s][* %F{cyan}%b%f'                                 # 通常時のフォーマット
zstyle ':vcs_info:*' actionformats '%s][* %F{cyan}%b%f(%F{red}%a%f)'              # コンフリクト時など,アクションがある場合のフォーマット
zstyle ':vcs_info:git:*' formats '%s][* %F{cyan}%u%c%b%f' '%m'                    # gitの通常時のフォーマット
zstyle ':vcs_info:git:*' actionformats '%s][* %F{cyan}%u%c%b%f(%F{red}%a%f)' '%m' # gitのコンフリクト時など,アクションがある場合のフォーマット
zstyle ':vcs_info:git:*' check-for-changes true                                   # %cと%uを有効にする
zstyle ':vcs_info:git:*' stagedstr "%f%F{green}"                                  # %cのフォーマット
zstyle ':vcs_info:git:*' unstagedstr "%f%F{red}"                                  # %uのフォーマット
zstyle ':vcs_info:git+set-message:*' hooks git-hook-begin git-file-check          # gitの状態チェック
# chpwd
zstyle ":chpwd:*" recent-dirs-max 100                                  # 最近利用したディレクトリ最大保存数
zstyle ":chpwd:*" recent-dirs-default true                             # 2つ以上の引数または数値以外の引数が与えられた場合,cdと同じ動作をする
zstyle ":completion:*" recent-dirs-insert always                       # recent-dirs-default trueの場合に補完を開始するとディレクトリ名一覧になる
# completion
zstyle ':completion:*' completer _complete _match _ignored _prefix     # コンプリータ指定(通常,パターンマッチ,除外パターン復活,単語途中の補完)
zstyle ':completion:*' menu select interactive                         # menuselect+interactive-mode(補完全般共通)
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}                  # 補完候補色付け

## prompt
# プロンプト表示
PROMPT='%{[38;5;33m%}_%{[0m%}
%{[38;5;33m%}┃%{[0m%}[${vcs_info_msg_0_}]:%~/
%{[38;5;33m%}┗━%{[0m%}(%?)%F{$PROMPT_VIM_MODE_COLOR}%#%f '
RPROMPT='[${vcs_info_msg_1_}|%D{%T}|%n]'

## command-line
# 色定義
zle_highlight=(region:standout special:standout suffix:bold isearch:bg=magenta,fg=white)

# vim: ft=zsh
